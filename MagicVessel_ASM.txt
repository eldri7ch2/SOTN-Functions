; Heart Vessel to Magic Vessel with Heart Max up retained
; Uses the same code space as heart max up logic
;
; Written by: MottZilla
;
; Code goes in BIN at 0x117B50

.nocash
.psx
.mips

org 800FE0E8h
movp    r2,[8003C9A0h]	; Load Player Character
nop
jz      r2,IsAlucard	; Jump if Alucard
nop
jmp     800FE39Ch	; Otherwise Return 1
mov   r2,1h
IsAlucard:
mov     r5,80090000h	; Quick Ptr
mov     r4,[r5+7BACh]	; Load Heart Max Value
mov	r6,[r5+7BA8h]	; Load Cur Heart Value
add     r4,5h		; Increase by 5
mov     [r5+7BACh],r4	; Write to Memory
add	r6,5h		; Increase by 5
mov     [r5+7BA8h],r6	; Write to Memory
mov     r4,[r5+7BB4h]	; Load MP Max
nop
add	r4,3h		; Increase by 3
mov     [r5+7BB0h],r4	; Set Current MP to Max
mov     [r5+7BB4h],r4	; Write new Max
nop
movp	r5,80137964h	; Trigger Fanfare Location for Heart max up
nop
mov	r4,[r5]		; Load Current
nop
add	r4,1		; Increase
mov	[r5],r4		; Write to memory

jmp     800FE39Ch	; Return 0
mov   r2,0h


end
